section \<open>Queue\<close>

text \<open>This section considers first-in first-out queues from three levels of abstraction.\<close>

theory Queue_Specs
  imports Main
begin

subsection \<open>High level\<close>

text \<open>On the high level, a queue is a list (@{type list}).\<close>

subsection \<open>Medium level\<close>

text \<open>On the medium level, a queue is specified via the following interface.\<close>

locale Queue =
  fixes empty :: "'q"
  fixes is_empty :: "'q \<Rightarrow> bool"
  fixes snoc :: "'q \<Rightarrow> 'a \<Rightarrow> 'q"
  fixes head :: "'q \<Rightarrow> 'a"
  fixes tail :: "'q \<Rightarrow> 'q"
  fixes invar :: "'q \<Rightarrow> bool"
  fixes list :: "'q \<Rightarrow> 'a list"
  assumes list_empty: "list empty = Nil"
  assumes is_empty: "invar q \<Longrightarrow> is_empty q = (list q = Nil)"
  assumes list_snoc: "invar q \<Longrightarrow> list (snoc q x) = list q @ [x]"
  assumes list_head: "\<lbrakk> invar q; list q \<noteq> Nil \<rbrakk> \<Longrightarrow> head q = hd (list q)"
  assumes list_tail: "\<lbrakk> invar q; list q \<noteq> Nil \<rbrakk> \<Longrightarrow> list (tail q) = tl (list q)"
  assumes invar_empty: "invar empty"
  assumes invar_snoc: "invar q \<Longrightarrow> invar (snoc q x)"
  assumes invar_tail: "\<lbrakk> invar q; list q \<noteq> Nil \<rbrakk> \<Longrightarrow> invar (tail q)"

lemma \<^marker>\<open>tag invisible\<close> (in Queue) list_queue:
  assumes "invar q"
  assumes "list q \<noteq> []"
  shows "list q = head q # list (tail q)"
  using assms
  by (simp add: list_head list_tail)

end