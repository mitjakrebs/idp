theory Graph_Ext
  imports
    AGF.Berge
begin

text \<open>This theory extends the formalization of undirected graphs.\<close>

section \<open>Graphs\<close>

type_synonym 'a vertex = 'a

type_synonym 'a edge = "'a vertex set"

type_synonym 'a graph = "'a edge set"

locale graph =
  fixes G :: "'a graph"
  assumes graph: "\<forall>e\<in>G. \<exists>u v. e = {u, v}"

lemma (in graph) mem_VsE:
  assumes "u \<in> Vs G"
  obtains v where
    "{u, v} \<in> G"
  using assms graph
  by (fastforce simp add: insert_commute elim: vs_member_elim)

lemma (in graph) graph_subset:
  assumes "G' \<subseteq> G"
  shows "graph G'"
  using graph assms
  by (force intro: graph.intro)

locale finite_graph = graph G for G +
  assumes finite_edges: "finite G"

lemma graphs_eqI:
  assumes "graph G1"
  assumes "graph G2"
  assumes "\<And>u v. {u, v} \<in> G1 \<longleftrightarrow> {u, v} \<in> G2"
  shows "G1 = G2"
  using assms
  by (auto simp add: graph_def)

subsection \<open>Vertices\<close>

lemma (in finite_graph) finite_vertices:
  shows "finite (Vs G)"
  using graph finite_edges
  by (auto simp add: Vs_def)

end